buildscript {
    ext {
        springBootVersion = '1.2.1.RELEASE'
    }
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.sourcemuse.gradle.plugin:gradle-mongo-plugin:0.4.0'
        classpath('org.grooscript:grooscript-gradle-plugin:0.10-SNAPSHOT') {
            exclude module: "groovy"
            exclude module: "groovy-json"
        }
    }
}

//plugins {
//  id "org.grooscript.conversion" version "0.9"
//}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'spring-boot' 
apply plugin: 'war'
apply plugin: 'mongo'
apply plugin: 'org.grooscript.conversion'

war {
    baseName = 'demo'
    version = '0.0.3-SNAPSHOT'
}
sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    providedRuntime
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-mongodb")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-websocket")
    compile("org.springframework:spring-messaging")
    compile("org.springframework.boot:spring-boot-starter-groovy-templates")
    compile("org.codehaus.groovy:groovy")
    compile("org.grooscript:grooscript:1.0.0-rc-2") {
        exclude module: "groovy"
        exclude module: "groovy-json"
    }
    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    compile 'cglib:cglib-nodep:2.2'
    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude module: 'groovy-all'
        exclude module: 'junit-dep'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

grooscript {
    source = ['src/main/groovy/component']
    destination = 'src/main/resources/static/js/app'
    classPath = ['src/main/groovy']
}

task bookConvertThread(type: org.grooscript.gradle.ThreadTask) {
    source = ['src/main/groovy/demo/model/Book.groovy']
    destination = 'src/main/resources/static/js/app'
    classPath = ['src/main/groovy']
}

task templatesListen(type: org.grooscript.gradle.TemplatesThreadTask) {
    templatesPath = 'src/main/resources/templates'
    templates = ['bookList.gtpl', 'bookTable.gtpl', 'lastBook.gtpl']
    destinationFile = 'src/main/resources/static/js/app/Templates.js'
    classPath = ['src/main/groovy']
}

spy {
    files = ['src/main/resources/templates', 'src/main/resources/static/css', 'src/main/resources/static/js/app']
    onChanges = { list ->
        springWebsocketTo 'http://localhost:8080/demo' data 'templates' onChannel '/app/reload'
    }
}

spyChanges.dependsOn templatesListen, thread, bookConvertThread
bootRun.dependsOn startManagedMongoDb, spyChanges
