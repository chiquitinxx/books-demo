buildscript {
    ext {
        springBootVersion = '1.1.9.RELEASE'
    }
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.sourcemuse.gradle.plugin:gradle-mongo-plugin:0.4.0'
        classpath('org.grooscript:grooscript-gradle-plugin:1.0.0-SNAPSHOT') {
            exclude module: "groovy"
            exclude module: "groovy-json"
        }
    }
}

//plugins {
//  id "org.grooscript.conversion" version "0.8"
//}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'spring-boot' 
apply plugin: 'war'
apply plugin: 'mongo'
apply plugin: 'org.grooscript.conversion'

war {
    baseName = 'demo'
    version = '0.0.2-SNAPSHOT'
}
sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    providedRuntime
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-mongodb")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-websocket")
    compile("org.springframework:spring-messaging")
    compile("org.springframework.boot:spring-boot-starter-groovy-templates")
    compile("org.codehaus.groovy:groovy")
    compile("org.grooscript:grooscript:1.0.0-SNAPSHOT") {
        exclude module: "groovy"
        exclude module: "groovy-json"
    }
    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    compile 'cglib:cglib-nodep:2.2'
    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude module: 'groovy-all'
        exclude module: 'junit-dep'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

grooscript {
    source = ['src/main/groovy/component/Colorable.groovy',
              'src/main/groovy/component/StompClient.groovy',
              'src/main/groovy/component/Visible.groovy',
              'src/main/groovy/component/Counter.groovy'
    ]
    destination = 'src/main/resources/static/js/app'
    classPath = ['src/main/groovy']
}

task templatesListen(type: org.grooscript.gradle.TemplatesThreadTask) {
    templatesPath = 'src/main/resources/templates'
    templates = ['bookList.gtpl']
    destinationFile = 'src/main/resources/static/js/app/Templates.js'
    classPath = ['src/main/groovy']
}

task templateChanging(type: org.grooscript.gradle.UpdatesTask) {
    files = ['src/main/resources/templates', 'src/main/resources/static/css']
    onChanges = { list->
        websocketTo('http://localhost:8080/demo').data('templates').via('/app/reload')
    }
}

bootRun.dependsOn startManagedMongoDb, templateChanging, templatesListen
